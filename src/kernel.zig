const std = @import("std");

const bss = @extern([*]u8, .{ .name = "__bss" });
const bss_end = @extern([*]u8, .{ .name = "__bss_end" });
const stack_top = @extern([*]u8, .{ .name = "__stack_top" });

const ram_start = @extern([*]u8, .{ .name = "__free_ram" });
const ram_end = @extern([*]u8, .{ .name = "__free_ram_end" });


const page_size = 4096;
var used_mem: usize = 0;
fn allocPages(pages: usize) []u8 {
    const ram = ram_start[0..(@intFromPtr(ram_end) - @intFromPtr(ram_start))];
    const alloc_size = pages * page_size;
    if (used_mem + alloc_size > ram.len) {
        @panic("out of memory");
    }
    const result = ram[used_mem..alloc_size];
    used_mem += alloc_size;
    @memset(result, 0);
    return result;
}

export fn kernel_main() noreturn {
    main() catch |err|  std.debug.panic("{s}", .{@errorName(err)});
    while (true) asm volatile ("wfi");

}

fn main() !void {
    const bss_len = @intFromPtr(bss_end) - @intFromPtr(bss);
    @memset(bss[0..bss_len], 0);

    const hello = "Hello kernel!\n";
    try console.print("{s}", .{hello});
    try console.print("1 + 2 = {d}, {x}\n", .{ 1 + 2, 0x1234abcd });

    // @panic("what do?");

    {
        _ = write_csr("stvec", @intFromPtr(&kernel_entry));
        // uncomment to trigger cpu exception
        // asm volatile ("unimp");
    }

    const one = allocPages(1);
    const two = allocPages(2);
    //will cause panic max pages availabel is 16384 . 64 * 1024 * 1024 / page_size
    //allocPages(16385);

    try console.print("one: {*} ({}), two: {*} ({})", .{ one.ptr, one.len, two.ptr, two.len });

}

export fn boot() linksection(".text.boot") callconv(.Naked) void {
    _ = asm volatile (
        \\mv sp, %[stack_top]
        \\j kernel_main
        :
        : [stack_top] "r" (stack_top),
    );
}

const SbiRet = struct { err: usize, value: usize };
const console: std.io.AnyWriter = .{ .context = undefined, .writeFn = write_fn };

fn write_fn(_: *const anyopaque, bytes: []const u8) !usize {
    for (bytes) |c| {
        _ = sbi(c, 0, 0, 0, 0, 0, 0, 1);
    }
    return bytes.len;
}

pub fn sbi(arg0: usize, arg1: usize, arg2: usize, arg3: usize, arg4: usize, arg5: usize, arg6: usize, arg7: usize) SbiRet {
    var err: usize = undefined;
    var value: usize = undefined;

    asm volatile ("ecall"
        : [err] "={a0}" (err),
          [value] "={a1}" (value),
        : [arg0] "{a0}" (arg0),
          [arg1] "{a1}" (arg1),
          [arg2] "{a2}" (arg2),
          [arg3] "{a3}" (arg3),
          [arg4] "{a4}" (arg4),
          [arg5] "{a5}" (arg5),
          [arg6] "{a6}" (arg6),
          [arg7] "{a7}" (arg7),
        : "memory"
    );

    return .{ .err = err, .value = value };
}

pub fn panic(msg: []const u8, error_return_trace: ?*std.builtin.StackTrace, ret_addr: ?usize) noreturn {
    _ = error_return_trace;
    _ = ret_addr;
    console.print("KERNEL PANIC: {s}\n", .{msg}) catch {};

    while (true) asm volatile ("wfi");
}

export fn kernel_entry() align(4) callconv(.Naked) void {
    asm volatile (
        \\addi sp, sp, -4*31
        \\sw ra, 4 * 0(sp)
        \\sw gp, 4 * 1(sp) 
        \\sw tp, 4 * 2(sp) 
        \\sw t0, 4 * 3(sp) 
        \\sw t1, 4 * 4(sp) 
        \\sw t2, 4 * 5(sp) 
        \\sw t3, 4 * 6(sp) 
        \\sw t4, 4 * 7(sp) 
        \\sw t5, 4 * 8(sp) 
        \\sw t6, 4 * 9(sp)
        \\sw a0, 4 * 10(sp) 
        \\sw a1, 4 * 11(sp) 
        \\sw a2, 4 * 12(sp) 
        \\sw a3, 4 * 13(sp) 
        \\sw a4, 4 * 14(sp) 
        \\sw a5, 4 * 15(sp) 
        \\sw a6, 4 * 16(sp) 
        \\sw a7, 4 * 17(sp)
        \\sw s0, 4 * 18(sp) 
        \\sw s1, 4 * 19(sp) 
        \\sw s2, 4 * 20(sp) 
        \\sw s3, 4 * 21(sp) 
        \\sw s4, 4 * 22(sp) 
        \\sw s5, 4 * 23(sp) 
        \\sw s6, 4 * 24(sp) 
        \\sw s7, 4 * 25(sp)
        \\sw s8, 4 * 26(sp) 
        \\sw s9, 4 * 27(sp) 
        \\sw s10, 4 * 28(sp) 
        \\sw s11, 4 * 29(sp)
        \\
        \\addi a0, sp, 4*31
        \\sw a0, -4(a0)
        \\
        \\mv a0,sp
        \\call handle_trap
        \\
        \\lw ra, 4 * 0(sp)
        \\lw gp, 4 * 1(sp) 
        \\lw tp, 4 * 2(sp) 
        \\lw t0, 4 * 3(sp) 
        \\lw t1, 4 * 4(sp) 
        \\lw t2, 4 * 5(sp) 
        \\lw t3, 4 * 6(sp) 
        \\lw t4, 4 * 7(sp) 
        \\lw t5, 4 * 8(sp) 
        \\lw t6, 4 * 9(sp)
        \\lw a0, 4 * 10(sp) 
        \\lw a1, 4 * 11(sp) 
        \\lw a2, 4 * 12(sp) 
        \\lw a3, 4 * 13(sp) 
        \\lw a4, 4 * 14(sp) 
        \\lw a5, 4 * 15(sp) 
        \\lw a6, 4 * 16(sp) 
        \\lw a7, 4 * 17(sp)
        \\lw s0, 4 * 18(sp) 
        \\lw s1, 4 * 19(sp) 
        \\lw s2, 4 * 20(sp) 
        \\lw s3, 4 * 21(sp) 
        \\lw s4, 4 * 22(sp) 
        \\lw s5, 4 * 23(sp) 
        \\lw s6, 4 * 24(sp) 
        \\lw s7, 4 * 25(sp)
        \\lw s8, 4 * 26(sp) 
        \\lw s9, 4 * 27(sp) 
        \\lw s10, 4 * 28(sp) 
        \\lw s11, 4 * 29(sp)
        \\lw sp, 4*30(sp)
        \\sret
    );
}

const TrapFrame = extern struct {
    ra: usize,
    gp: usize,
    tp: usize,
    t0: usize,
    t1: usize,
    t2: usize,
    t3: usize,
    t4: usize,
    t5: usize,
    t6: usize,
    a0: usize,
    a1: usize,
    a2: usize,
    a3: usize,
    a4: usize,
    a5: usize,
    a6: usize,
    a7: usize,
    s0: usize,
    s1: usize,
    s2: usize,
    s3: usize,
    s4: usize,
    s5: usize,
    s6: usize,
    s7: usize,
    s8: usize,
    s9: usize,
    s10: usize,
    s11: usize,
    sp: usize,
};

export fn handle_trap(tf: *TrapFrame) void {
    _ = tf;
    const scause = read_csr("scause");
    const stval = read_csr("stval");
    const user_pc = read_csr("sepc");

    std.debug.panic("UnExpected trap scause={x}, stval={x}, user_pc={x}", .{ scause, stval, user_pc });
}

fn read_csr(comptime reg: []const u8) usize {
    return asm volatile ("csrr %[ret], " ++ reg
        : [ret] "=r" (-> usize),
    );
}

fn write_csr(comptime reg: []const u8, val: usize) void {
    return asm volatile ("csrw " ++ reg ++ ", %[val]"
        :
        : [val] "r" (val),
    );
}
